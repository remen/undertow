plugins {
    id 'java'
    id 'application'
    id "org.mikeneck.junit.starter.library" version "5.0.2"
    id "org.ajoberstar.grgit" version "2.1.0"
}

applicationName = 'app' // For convenience in Dockerfile
mainClassName = 'com.meltwater.owl.hello.Application'
ext.commit = grgit.head().getAbbreviatedId() + (grgit.status().clean ? "" : "-dirty")
version = new Date().format("yyyyMMdd'T'HHmmss") + "-$commit"

distTar.enabled = false
distZip.enabled = false
assemble.dependsOn(installDist)

ext.dockerTag = "meltwater-docker-platform-registry.bintray.io/${project.name}:${project.version}"
task("dockerBuild", type: Exec) {
    group = 'Docker'
    description = 'Builds a docker image from this project'

    inputs.files "Dockerfile"
    inputs.files tasks.installDist.outputs

    commandLine 'docker', 'build', '-t', "${dockerTag}", "."
}

repositories {
    jcenter()
}

dependencies {
    compile 'io.undertow:undertow-core:1.4.12.Final'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile 'net.logstash.logback:logstash-logback-encoder:4.11'


    // TEST DEPENDENCIES
    // -----------------
    // To avoid compiler warnings about @API annotations in JUnit code
    testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'

    testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.4'
    testCompile group: 'com.google.guava', name: 'guava', version: '23.6-jre'
}

